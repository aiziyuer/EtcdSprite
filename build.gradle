// 主仓库
repositories {
    jcenter()
    maven { url 'repo' }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.edu.sc.seis.gradle:launch4j:2.3.0"
    }
}

String swtGroupId = 'org.eclipse.swt', swtVersion = '3.7.2'
def osDependencies = []
if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {

    println System.properties['os.arch']
    println System.properties['sun.arch.data.model']

    // 区分32位和64位
    String arch = System.getenv("PROCESSOR_ARCHITECTURE")
    String wow64Arch = System.getenv("PROCESSOR_ARCHITEW6432")
    Boolean is64Arch = (arch && arch.endsWith("64")) \
                             || (wow64Arch && wow64Arch.endsWith("64"))

    // 这里除了需要看系统外, 还需要看jvm是否是64的, 防止32位启动64无法正常运行
    String jvmArch = System.properties['sun.arch.data.model']
    is64Arch = is64Arch && jvmArch && jvmArch.endsWith("64")

    if (is64Arch) {
        osDependencies << String.format("%s:%s:%s", swtGroupId, 'org.eclipse.swt.win32.win32.x86_64', swtVersion)
    } else {
        osDependencies << String.format("%s:%s:%s", swtGroupId, 'org.eclipse.swt.win32.win32.x86', swtVersion)
    }

} else {

    // 全部编译
    osDependencies << String.format("%s:%s:%s", swtGroupId, 'org.eclipse.swt.win32.win32.x86_64', swtVersion)
}

////// -----------------------Java编译依赖配置开始 ---------------------------//////////
apply plugin: 'java'

// 显示定义工程源码结构
sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/java"]
            includes = ["**/*.xwt"]
        }
    }
    test {
        java {
            srcDirs = ["src/test/java"]
        }
    }
}


dependencies {
    // The production code uses Guava
    compile 'com.google.guava:guava:20.0'

    compileOnly "org.projectlombok:lombok:1.12.6"

    // 系统相关的依赖
    compile osDependencies

    // JFace类库
    compile 'org.eclipse.jface:org.eclipse.jface:3.7.0.v20110928-1505'
    compile 'org.eclipse.core:org.eclipse.core.commands:3.6.0.I20110111-0800'
    compile 'org.eclipse.osgi:org.eclipse.osgi:3.7.2.v20120110-1415'

    // databing相关的几个类库
    compile files('lib/com.ibm.icu_4.4.2.v20110823.jar')
    compile files('lib/org.eclipse.core.databinding_1.4.0.I20110111-0800.jar')
    compile files('lib/org.eclipse.core.databinding.beans_1.2.100.I20100824-0800.jar')
    compile files('lib/org.eclipse.core.databinding.observable_1.4.0.I20110222-0800.jar')
    compile files('lib/org.eclipse.core.databinding.property_1.4.0.I20110222-0800.jar')
    compile files('lib/org.eclipse.jface.databinding_1.5.0.I20100907-0800.jar')

    // xwt类库
    compile files('lib/org.eclipse.e4.xwt_0.9.1.SNAPSHOT.jar')
    compile files('lib/org.eclipse.e4.xwt.forms_0.9.1.SNAPSHOT.jar')
    compile files('lib/org.pushingpixels.trident_1.2.0.v20100204-1500.jar')

    // 配置Log4j
    String LOG4J_VERSION = '2.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: LOG4J_VERSION
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: LOG4J_VERSION
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: LOG4J_VERSION
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: LOG4J_VERSION
    compile group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: LOG4J_VERSION
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.7'
    compile group: 'org.jboss.logging', name: 'jboss-logging', version: '3.2.0.Final'

    // 配置Spring
    String SPRING_VERSION = '4.3.3.RELEASE'
    compile group: 'org.springframework', name: 'spring-context', version: SPRING_VERSION

    // 配置数据库
    compile group: 'org.springframework', name: 'spring-jdbc', version: SPRING_VERSION
    compile group: 'org.springframework', name: 'spring-orm', version: SPRING_VERSION
    compile group: 'org.hibernate', name: 'hibernate-core', version: '4.3.11.Final'
    compile 'com.enigmabridge:hibernate4-sqlite-dialect:0.1.2'
    compile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.0'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.16.1'

    // 通用工具包
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.8.3'

    // 测试用框架
    testCompile 'junit:junit:4.12'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

////// -----------------------Java编译依赖配置开始 ---------------------------//////////

///// ------------------------打包配置开始---------------------------------///////
apply plugin: "edu.sc.seis.launch4j"

String APP_NAME = 'EtcdSprite'
String PACKAGE_DIR = String.format("%s/%s", buildDir, APP_NAME)

task copyStaticFile(type: Copy) {

    copy {
        // 拷贝图标
        from 'icons/etcd-seeklogo.com.ico'
        into PACKAGE_DIR + '/icons'
        rename { fileName -> APP_NAME + '.ico' }
    }

    copy {
        // 拷贝配置文件
        from 'config/log4j2.xml'
        into PACKAGE_DIR + '/config'
    }

    copy {
        // 拷贝应用配置文件
        from 'config/template.l4j.ini'
        into PACKAGE_DIR
        rename { fileName -> APP_NAME + '.l4j.ini' }
    }

    copy {
        // 配置Spring文件
        from 'config/spring'
        into PACKAGE_DIR + '/config/spring'
    }

    copy {
        // 拷贝数据库文件
        from 'db/app.db'
        into PACKAGE_DIR + '/db'
    }
}

tasks.createExe.dependsOn('copyStaticFile')

launch4j {
    outputDir = APP_NAME
    mainClassName = "com.aiziyuer.app.CoreApplication"
    icon = String.format("%s/icons/%s.ico", PACKAGE_DIR, APP_NAME)
}
///// ------------------------打包配置开始---------------------------------///////


//// -------------------------发布配置开始--------------------------------///////
task zipDist(type: Zip){
    // 指定需要压缩的目录
    from "${buildDir}/${APP_NAME}"

    // 指定发布前需要哪个执行哪个任务
    dependsOn { [ "createExe"] }
}
//// -------------------------发布配置结束--------------------------------///////
